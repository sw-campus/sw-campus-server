plugins {
    id 'java'
    id 'org.springframework.boot' apply false
    id 'io.spring.dependency-management' apply false
    id 'eclipse'
}

// VSCode Java Extension 지원을 위한 Eclipse 프로젝트 파일 생성
allprojects {
    apply plugin: 'eclipse'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

allprojects {
    group = projectGroup
    version = applicationVersion

    repositories {
        mavenCentral()
    }
}

subprojects {
    // 전파가 필요한 모듈에서는 java-library + api 사용
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'

    // Java 컴파일 옵션: 파라미터 이름 정보 보존 + UTF-8 인코딩
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.add('-parameters')
        options.encoding = 'UTF-8'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    dependencies {
        // 공통 Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        // 공통 로깅 API
        implementation 'org.slf4j:slf4j-api'

        // 공통 테스트 의존성
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':sw-campus-api') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation project(':sw-campus-infra:db-postgres')
    }

    bootJar {
        enabled = true
    }

    jar {
        enabled = false
    }
}
